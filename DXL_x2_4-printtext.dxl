// Lab 2.4 - Print text

/*
Manually open the "System requirements" module
write and test a script to do the following:

For each object in the display set, print the following:
    Object Identifier
    Object Number & Heading - if the object has a heading
    Object Text - if the object has text
*/

Object o
string hdg, txt
for o in current Module do 
{
    print identifier(o) "\t" 
    hdg = o."Object Heading"
    
    if(!null(hdg))     
    {
        print number(o) "  " hdg "\n"
    }
    
    txt = o."Object Text"
    if(txt != "" ) 
    {
        print txt "\n"
    }
}

/////////////////////////////////////////
// Alternative: use buffers instead
Buffer b = create()
Object o
string hdg, txt
for o in current Module do 
{
    b += identifier(o) "\t"
	hdg = o."Object Heading"
    if(!null(hdg))
    {
        b += number(o) "  " hdg "\n"
    }
    txt = o."Object Text"
    if(txt != "" ) 
    {
        b += txt "\n"
    }
}
print stringOf(b)  // print tempStringOf(b)
delete b




//////////////////////////////////////////////////
// As function


string getHeading(Object o)
{
	string returnValue = null
	if(!null(o."Object Heading" "") )
    {
        returnValue =  number(o) "  " o."Object Heading" "\n"
    }
    
    return returnValue
}

string getText(Object o)
{
	string returnValue = null
	if(!null(o."Object Text" "") )
    {
        returnValue = "  " o."Object Text" "\n"
    }
    
    return returnValue
}

void printModule(Module m)
{
	Buffer b = create()
	Object o
	for o in m do 
	{
	    b += identifier(o) "\t"
	    b += getHeading(o)
	    b += getText(o)
	    
	}
	print stringOf(b)
	delete b
}
printModule(current Module)